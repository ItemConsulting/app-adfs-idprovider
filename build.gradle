plugins {
    id 'maven-publish'
    id 'com.enonic.defaults' version '2.0.1'
    id 'com.enonic.xp.app' version '2.0.0'
}

app {
    name = project.appName
    displayName = 'OAuth ID Provider'
    vendorName = 'Enonic AS'
    vendorUrl = 'http://enonic.com'
    systemVersion = "${xpVersion}"
}

dependencies {
    compile "com.enonic.xp:core-api:${xpVersion}"
    compile "com.enonic.xp:portal-api:${xpVersion}"
    include "com.enonic.xp:lib-auth:${xpVersion}"
    include "com.enonic.xp:lib-context:${xpVersion}"
    include "com.enonic.xp:lib-io:${xpVersion}"
    include "com.enonic.xp:lib-portal:${xpVersion}"
    include "com.enonic.lib:lib-thymeleaf:2.0.0"
    include 'com.enonic.lib:lib-http-client:2.0.0'
    include 'com.enonic.lib:text-encoding:1.1.0'
    include 'com.enonic.lib:lib-util:2.0.0'
    include( 'com.auth0:mvc-auth-commons:1.0.0' ) {
        exclude group: 'javax.servlet', module: 'javax.servlet-api'
        exclude group: 'com.google.guava', module: 'guava'
    }
    include 'org.apache.httpcomponents:httpclient:4.5'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    xp.enonicRepo()
}

//──────────────────────────────────────────────────────────────────────────────
// Generate documentation
//──────────────────────────────────────────────────────────────────────────────
task generate_docs( type: Exec ) {
    executable 'npm'
    args = ['run', 'generate-docs']
    mustRunAfter 'build'
}

if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
    task copy_urijs( type: Copy ) {
        from fileTree(dir: "node_modules/urijs", includes: ["**/*.js", "**/*.json"]).files
        into 'build/resources/main/lib/urijs'
    }
} else {
    task copy_urijs( type: Exec ) {
        executable 'npm'
        args = ['run', 'copyUriJs']
    }
}
processResources.dependsOn.add( copy_urijs )

